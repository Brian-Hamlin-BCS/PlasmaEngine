[RunInEditor]
class PostProcessDOF : LightningComponent
{
  [Property] var Enable : Boolean = true;
  // Point of Focus from camera position positive viewspace distance ahead.
  [Group("Settings")]
  [Property][Slider(1,16,1)] var BlurAmmount : Real = 16.0;
  [Group("Settings")]
  [Property] var BlurBokeh : Boolean = true;
  [Group("Settings")]
  [Property] var HalfResolutionBokeh = false;
  
  
  [Group("Debug")]
  [Property] var Debug : Boolean = false;
  [Group("Debug")]
  [Property] var DebugType : DebugType = DebugType.COC;
  
  function OnRenderTasksUpdate(event : RenderTasksEvent, depthTarget : Texture, colorTarget : RenderTarget)
  {
    if(this.Enable == false)
      return;
      
    var camera = event.CameraViewportCog.Camera;
    if(camera == null)
      return;
      
    var size = event.ViewportSize;
    
    var sizeBokeh = size;
    if(this.HalfResolutionBokeh)
      sizeBokeh /= 2;
    
    var samplerSettings = SamplerSettings();
    samplerSettings.AddressingX = TextureAddressing.Repeat;
    samplerSettings.AddressingY = TextureAddressing.Repeat;
    
    var dofBuffer = event.GetRenderTarget(size, TextureFormat.RGBA16f, samplerSettings);
    var bokehBuffer = event.GetRenderTarget(sizeBokeh, TextureFormat.RGBA16f, samplerSettings);
    
    var cocPass = COCPass();
    cocPass.Aperture = camera.Aperture;
    cocPass.FocusDistance = camera.FocalDistance;
    cocPass.ScreenBuffer = colorTarget.Texture;
    cocPass.Depth = depthTarget;
    event.AddRenderTaskPostProcess(dofBuffer, cocPass, "Dof COC");
    
    var bokehPass = BokehPass();
    bokehPass.DOFBuffer = colorTarget.Texture;
    bokehPass.DOFSampleCount = this.BlurAmmount as Integer;
    event.AddRenderTaskPostProcess(bokehBuffer, bokehPass, "Dof Bokeh");
    
    if(this.BlurBokeh)
      BlurTexture[SeparableBlurRadius5].Call(event, bokehBuffer);
        
    var tentPass = TentPass();
    tentPass.BokehBuffer = bokehBuffer.Texture;
    event.AddRenderTaskPostProcess(bokehBuffer, tentPass, "Dof TentPass");

    var combinePass = DOFCombinePass();
    combinePass.DOFBuffer = dofBuffer.Texture;
    combinePass.ScreenBuffer = colorTarget.Texture;
    combinePass.BokehBuffer = bokehBuffer.Texture;
    event.AddRenderTaskPostProcess(colorTarget, combinePass, "Dof Combine");
    
    if(this.Debug)
    {
      if(this.DebugType == DebugType.COC)
      {
        var dofDebug = DOFDebug();
        dofDebug.DOFBuffer = dofBuffer.Texture;
        event.AddRenderTaskPostProcess(colorTarget, dofDebug, "Dof Debug");
      }
      else
      {
        var copyPass = CopyTarget();
        copyPass.Texture = bokehBuffer.Texture;
        event.AddRenderTaskPostProcess(colorTarget, copyPass, "Dof Debug");
      }
    }
    
    dofBuffer.Release();
    bokehBuffer.Release();
  }
}

enum DebugType
{
  COC,
  Bokeh
}